;;;;;;;;;;;;;;;;;;;
;; Global Variables

global FILE_PATH
global ROOT_NODE
global JUMP_TO_SCOPE_NODE

;;;;;;;;;;;;;;;;;;;;;;;
;; Attribute Shorthands

attribute node_definition = node        => type = "pop_symbol", node_symbol = node, is_definition
attribute node_reference = node         => type = "push_symbol", node_symbol = node, is_reference
attribute pop_node = node               => type = "pop_symbol", node_symbol = node
attribute pop_scoped_node = node        => type = "pop_scoped_symbol", node_symbol = node
attribute pop_scoped_symbol = symbol    => type = "pop_scoped_symbol", symbol = symbol
attribute pop_symbol = symbol           => type = "pop_symbol", symbol = symbol
attribute push_node = node              => type = "push_symbol", node_symbol = node
attribute push_scoped_node = node       => type = "push_scoped_symbol", node_symbol = node
attribute push_scoped_symbol = symbol   => type = "push_scoped_symbol", symbol = symbol
attribute push_symbol = symbol          => type = "push_symbol", symbol = symbol
attribute scoped_node_definition = node => type = "pop_scoped_symbol", node_symbol = node, is_definition
attribute scoped_node_reference = node  => type = "push_scoped_symbol", node_symbol = node, is_reference
attribute symbol_definition = symbol    => type = "pop_symbol", symbol = symbol, is_definition
attribute symbol_reference = symbol     => type = "push_symbol", symbol = symbol, is_reference
attribute node_symbol = node            => symbol = (source-text node), source_node = node
attribute defkind = kind                => debug_defkind = kind
attribute refkind = kind                => debug_refkind = kind

;; program
(program)@program {
  node @program.scope
  node @program.defs
  node @program.central
  node @program.exit
  edge ROOT_NODE -> @program.central
  edge @program.central -> @program.scope
  edge @program.scope -> ROOT_NODE
  edge @program.scope -> @program.exit
  edge @program.scope -> @program.defs
}

(program (_)@statement)@program {
  edge @program.defs -> @statement.defs
  edge @statement.scope -> @program.scope
}

(program (package_declaration (identifier)@id))@program {
  attr (@program.central)
    node_definition = @id,
    defkind = "package"
  attr (@program.exit)
    push_node = @id
  edge @program.exit -> ROOT_NODE
}

(program (package_declaration (scoped_identifier)@id))@program {
  attr (@program.central)
    node_definition = @id,
    defkind = "package"

  edge ROOT_NODE -> @id.pop_bottom
  edge @id.pop_top -> @program.scope

  edge @program.exit -> @id.push_top
  edge @id.push_bottom -> ROOT_NODE
}

;; imports
(import_declaration
 (identifier)@thing
)@import {
  node @import.scope
  node @import.defs
  node ref
  attr (ref)
    node_reference = @thing,
    refkind = "includes"

  edge ref -> @import.scope
  edge @import.defs -> ref
}

(import_declaration
 (scoped_identifier)@thing
)@import {
  node @import.scope
  node @import.defs
  attr (@thing.push_top)
    refkind = "includes",
    is_reference

  edge @thing.push_bottom -> @import.scope
  edge @import.defs -> @thing.push_bottom
}

;; packages
(package_declaration)@package {
  node @package.scope
  node @package.defs
}

;; classes
(class_declaration
  name: (_)@id
  body: (_)@body
)@class {
  node @class.scope
  node @class.defs
  edge @class.scope -> @class.defs

  node @class.central
  attr (@class.central)
    node_definition = @id,
    defkind = "class"
  edge @class.defs -> @class.central

  edge @body.scope -> @class.scope
  edge @class.central -> @body.defs
}

(class_declaration
  superclass: (superclass (type_identifier)@superclass)
)@class {
  node ref
  attr (ref)
    node_reference = @superclass,
    refkind = "extends"

  edge ref -> @class.scope
  edge @class.central -> ref
}

(class_declaration
  superclass: (superclass (scoped_type_identifier)@superclass)
)@class {
  attr (@superclass.push_top)
    refkind = "extends",
    is_reference

  edge @superclass.push_bottom -> @class.scope
  edge @class.central -> @superclass.push_bottom
}

(class_declaration
  interfaces: (super_interfaces (type_list (type_identifier)@superinterface))
)@class {
  node ref
  attr (ref)
    node_reference = @superinterface,
    refkind = "implements"

  edge ref -> @class.scope
  edge @class.central -> ref
}

(class_declaration
  interfaces: (super_interfaces (type_list (scoped_type_identifier)@superinterface))
)@class {
  attr (@superinterface.push_top)
    refkind = "implements",
    is_reference

  edge @superinterface.push_bottom -> @class.scope
  edge @class.central -> @superinterface.push_bottom
}

;; class body
(class_body)@body {
  node @body.scope
  node @body.defs
}

(class_body (_)@statement)@body {
  edge @statement.scope -> @body.scope
  edge @body.defs -> @statement.defs
}

;; interfaces
(interface_declaration
  name: (_)@id
  body: (_)@body
)@interface {
  node @interface.scope
  node @interface.defs
  edge @interface.scope -> @interface.defs

  node @interface.central
  attr (@interface.central)
    node_definition = @id,
    defkind = "interface"
  edge @interface.defs -> @interface.central

  edge @body.scope -> @interface.scope
  edge @interface.central -> @body.defs
}

(interface_declaration
  (extends_interfaces (type_list (type_identifier)@superinterface))
)@interface {
  node ref
  attr (ref)
    node_reference = @superinterface,
    refkind = "extends"

  edge ref -> @interface.scope
  edge @interface.central -> ref
}

(interface_declaration
  (extends_interfaces (type_list (scoped_type_identifier)@superinterface))
)@interface {
  attr (@superinterface.push_top)
    refkind = "extends",
    is_reference

  edge @superinterface.push_bottom -> @interface.scope
  edge @interface.central -> @superinterface.push_bottom
}

;; interface body
(interface_body)@body {
  node @body.scope
  node @body.defs
}

(interface_body (_)@statement)@body {
  edge @statement.scope -> @body.scope
  edge @body.defs -> @statement.defs
}

;; scoped type identifier
[
  (scoped_type_identifier
    (type_identifier)@scope
    (type_identifier)@name
  )@scoped
  (scoped_identifier
    (identifier)@scope
    (identifier)@name
  )@scoped
]
{
  node push_scope
  attr (push_scope)
    push_node = @scope
  
  node push_name
  attr (push_name)
    push_node = @name

  edge push_name -> push_scope
  edge push_scope -> push_name
  let @scoped.push_top = push_name
  let @scoped.push_bottom = push_scope

  node pop_scope
  attr (pop_scope)
    pop_node = @scope
  
  node pop_name
  attr (pop_name)
    pop_node = @name

  edge pop_name -> pop_scope
  edge pop_scope -> pop_name
  let @scoped.pop_top = pop_name
  let @scoped.pop_bottom = pop_scope
}

[
  (scoped_type_identifier
    (scoped_type_identifier)@scope
    (type_identifier)@name
  )@scoped
  (scoped_identifier
    (scoped_identifier)@scope
    (identifier)@name
  )@scoped
] {
  node push_name
  attr (push_name)
    push_node = @name

  edge push_name -> @scope.push_top
  edge @scope.push_top -> push_name
  let @scoped.push_top = push_name
  let @scoped.push_bottom = @scope.push_bottom
  
  node pop_name
  attr (pop_name)
    pop_node = @name

  edge pop_name -> @scope.pop_top
  edge @scope.pop_top -> pop_name
  let @scoped.pop_top = pop_name
  let @scoped.pop_bottom = @scope.pop_bottom
}

;; method declaration
(method_declaration
  name: (_)@name
)@method {
  node @method.scope
  node @method.defs
  node @method.central

  attr (@method.central)
    node_definition = @name,
    defkind = "function"
  
  edge @method.scope -> @method.defs
  edge @method.defs -> @method.central
}

(method_declaration
  parameters: (formal_parameters (_)@parameter)
)@method {
  edge @method.central -> @parameter.defs
  edge @parameter.scope -> @method.scope
}

(method_declaration
  body: (_)@body
)@method {
  edge @body.scope -> @method.scope
  edge @method.central -> @body.defs
}

(method_declaration
  type: (type_identifier)@type
)@method {
  node ref
  attr (ref)
    node_reference = @type,
    refkind = "usesType"

  edge ref -> @method.scope
  edge @method.central -> ref
}

(method_declaration
  type: (scoped_type_identifier)@type
)@method {
  attr (@type.push_top)
    refkind = "usesType",
    is_reference

  edge @type.push_bottom -> @method.scope
  edge @method.central -> @type.push_bottom
}

;; constructor declaration
(constructor_declaration
  name: (_)@name
)@constructor {
  node @constructor.scope
  node @constructor.defs
  node @constructor.central

  attr (@constructor.central)
    node_definition = @name,
    defkind = "function"
  
  edge @constructor.scope -> @constructor.defs
  edge @constructor.defs -> @constructor.central
}

(constructor_declaration
  parameters: (formal_parameters (_)@parameter)
)@constructor {
  edge @constructor.central -> @parameter.defs
  edge @parameter.scope -> @constructor.scope
}

(constructor_declaration
  body: (_)@body
)@constructor {
  edge @body.scope -> @constructor.scope
  edge @constructor.central -> @body.defs
}

;; local variable declaration
(local_variable_declaration
  declarator: (_)@declarator
)@variable {
  node @variable.scope
  node @variable.defs
  node @variable.central

  attr (@variable.central)
    node_definition = @declarator,
    defkind = "attribute"
  
  edge @variable.scope -> @variable.defs
  edge @variable.defs -> @variable.central
}

(local_variable_declaration
  type: (type_identifier)@type
)@variable {
  node ref
  attr (ref)
    node_reference = @type,
    refkind = "usesType"

  edge ref -> @variable.scope
  edge @variable.central -> ref
}

(local_variable_declaration
  type: (scoped_type_identifier)@type
)@variable {
  attr (@type.push_top)
    refkind = "usesType",
    is_reference

  edge @type.push_bottom -> @variable.scope
  edge @variable.central -> @type.push_bottom
}

;; field declaration
(field_declaration
  declarator: (_)@declarator
)@field {
  node @field.scope
  node @field.defs
  node @field.central

  attr (@field.central)
    node_definition = @declarator,
    defkind = "attribute"
  
  edge @field.scope -> @field.defs
  edge @field.defs -> @field.central
}

(field_declaration
  type: (type_identifier)@type
)@field {
  node ref
  attr (ref)
    node_reference = @type,
    refkind = "usesType"

  edge ref -> @field.scope
  edge @field.central -> ref
}

(field_declaration
  type: (scoped_type_identifier)@type
)@field {
  attr (@type.push_top)
    refkind = "usesType",
    is_reference

  edge @type.push_bottom -> @field.scope
  edge @field.central -> @type.push_bottom
}

;; formal parameter
(formal_parameter
  name: (_)@name
)@parameter {
  node @parameter.scope
  node @parameter.defs
  node @parameter.central

  attr (@parameter.central)
    node_definition = @name,
    defkind = "parameter"
  
  edge @parameter.scope -> @parameter.defs
  edge @parameter.defs -> @parameter.central
}

(formal_parameter
  type: (type_identifier)@type
)@parameter {
  node ref
  attr (ref)
    node_reference = @type,
    refkind = "usesType"

  edge ref -> @parameter.scope
  edge @parameter.central -> ref
}

(formal_parameter
  type: (scoped_type_identifier)@type
)@parameter {
  attr (@type.push_top)
    refkind = "usesType",
    is_reference

  edge @type.push_bottom -> @parameter.scope
  edge @parameter.central -> @type.push_bottom
}

;; block
(block)@block {
  node @block.scope
  node @block.defs
}

(block (_)@statement)@block {
  edge @statement.scope -> @block.scope
  edge @block.defs -> @statement.defs
}

;; constructor body
(constructor_body)@body {
  node @body.scope
  node @body.defs
}

(constructor_body (_)@statement)@body {
  edge @statement.scope -> @body.scope
  edge @body.defs -> @statement.defs
}

;; comments
(line_comment)@line_comment {
  node @line_comment.defs
  node @line_comment.scope
}

(block_comment)@block_comment {
  node @block_comment.defs
  node @block_comment.scope
}